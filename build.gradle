buildscript {
  repositories {
    jcenter()
  }
  dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0"
  }
}

apply plugin: "java"
apply plugin: "maven-publish"
apply plugin: "eclipse"

repositories {
  jcenter()
}

dependencies {
  compile "commons-lang:commons-lang:2.6"
  compile "com.google.guava:guava:19.0"
  compile "com.sparkjava:spark-core:2.5"
  compile "commons-cli:commons-cli:1.3.1"
  compile "org.apache.commons:commons-math3:3.6.1"
  compile "com.fasterxml.jackson.core:jackson-databind:2.8.4"
  
  runtime "ch.qos.logback:logback-classic:1.1.7"

  testCompile "org.mockito:mockito-core:1.10.19"
  testCompile "junit:junit:4.12"
}

task("sourceJar", type: Jar) {
  classifier = "sources"
  from sourceSets.main.allJava
}

artifacts {
  archives sourceJar
}

test {
  useJUnit {
    excludeCategories "org.roy.loadx.test.ManualTest"
  }
  testLogging {
    events "passed", "skipped", "failed", "standardError"
  }
}

if (hasProperty("artifactory_contextUrl")) {
  apply plugin: "com.jfrog.artifactory"

  artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
      repository {
        repoKey = "${artifactory_repoKey}"
        username = "${artifactory_username}"
        password = "${artifactory_password}"
        maven = true
      }
      defaults {
        publications ('mavenJava')
      }
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from project.components.java
        artifactId = "loadx"
        artifact project.sourceJar {
          classifier "sources"
        }
      }
    }
  }
}
