plugins {
  id "com.jfrog.artifactory" version "4.0.0"
}

apply plugin: 'java'
apply plugin: "maven-publish"

repositories {
  jcenter()
}

dependencies {
  compile 'org.springframework:spring-context:4.2.2.RELEASE'

  testCompile 'org.springframework:spring-test:4.2.2.RELEASE'
  testCompile 'org.jmockit:jmockit:1.20'
  testCompile 'junit:junit:4.12'
}

task('sourceJar', type: Jar) {
  from { sourceSets.main.allJava }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from project.components.java
      artifact sourceJar {
        classifier "sources"
      }
    }
  }
}

// Configures task: artifactoryPublish
// Create a gradle.properties in your HOMEDIR/.gradle with these four properties:
//   artifactoryContextUrl, artifactoryRepoKey, artifactoryUsername, and artifactoryPassword
artifactory {
  contextUrl = "${artifactoryContextUrl}"
  publish {
    repository {
      repoKey = "${artifactoryRepoKey}"
      username = "${artifactoryUsername}"
      password = "${artifactoryPassword}"
    }
    defaults {
      publications ('mavenJava')
    }
  }
}